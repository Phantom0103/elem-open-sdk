plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.phantom0103'
version '2.5.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.75'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = 'Central OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = project.findProperty("sonatype.username")
                password = project.findProperty("sonatype.password")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'elem-open-sdk'
                description = '饿了么零售开放平台Java-SDK'
                url = 'https://github.com/phantom0103/elem-open-sdk'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'zhouw'
                        email = ''
                    }
                }

                scm {
                    url = 'https://github.com/Phantom0103/elem-open-sdk'
                    connection = 'scm:git:git://github.com/phantom0103/elem-open-sdk.git'
                    developerConnection = 'scm:git:ssh://git@github.com:phantom0103/elem-open-sdk.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.encoding = "UTF-8"
    options.tags = ["Author", "Description", "description", "Date", "date"]
}